//file:noinspection GrUnresolvedAccess
//file:noinspection GradlePackageUpdate
//file:noinspection GroovyAccessibility
//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyUnusedAssignment
//file:noinspection unused

class Globals {
    static def plugin_name = "glob-mc-homes"
    static def plugin_version = "./src/build/sem-ver".execute().text.trim()
    // run `make update` to update tooling versions
    static def target_java_version = 17
    static def jdk_version = "17.0.10+7"
    static def google_java_format_version = "1.19.2"
    static def checkstyle_version = "10.13.0"
}

plugins {
    id 'java'
}

group = 'au.com.glob'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(Globals.target_java_version)
}

processResources {
    def props = [version: Globals.plugin_version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
